# -*-awk-*-
BEGIN {
    header()
    reset()
}

/__BEGIN_FILE__/ {
    sub(/__BEGIN_FILE__ /, "", $0)
    split($0, parts, "___")

    data["Graph"] = parts[1]
    data["NumNodes"] = -1
    data["NumMPIsPerNode"] = -1
    data["NumThreadsPerMPI"] = -1
    data["Seed"] = -1
    data["Epsilon"] = 0.03

    split(parts[2], parts, "_")
    for (i in parts) {
        if (match(parts[i], /k([0-9]+)/, m)) {
            data["K"] = m[1]
        } else if (match(parts[i], /P([0-9]+)x([0-9]+)x([0-9]+)/, m)) {
            data["NumNodes"] = m[1]
            data["NumMPIsPerNode"] = m[2]
            data["NumThreadsPerMPI"] = m[3]
        } else if (match(parts[i], /seed([0-9]+)/, m)) {
            data["Seed"] = m[1]
        } else if (match(parts[i], /eps([0-9\.\-e]+)/, m)) {
            data["Epsilon"] = m[1]
        }
    }
}

/__END_FILE__/ {
    yield()
}

match($0, /`-- Partitioning: \.* *([0-9\.e\-]+) s/, m) {
    data["Time"] = m[1]
}
match($0, /\|-- Partitioning: \.* *([0-9\.e\-]+) s/, m) {
    data["Time"] = m[1]
}

match($0, /  Imbalance: *([0-9\.e\-]+)/, m) {
    data["Balance"] = m[1]
}

match($0, /  Edge cut: *([0-9]+)/, m) {
    data["Cut"] = m[1]
}

match($0, /\[STATS:FM:BATCHES\]/, m) {
    resetBatchesF()
    level += 1
}

match($0, /Max distance: *([0-9]+) *\[STATS:FM:BATCHES:[0-9]+\]/, m) {
    if (m[1] > batchesMaxDistanceA) {
        batchesMaxDistanceA = m[1]
    }
    if (level == 0 && m[1] > batchesMaxDistanceC) {
        batchesMaxDistanceC = m[1]
    }
    if (m[1] > batchesMaxDistanceF) {
        batchesMaxDistanceF = m[1]
    }
}

match($0, /Size by distance: *([0-9]+(,[0-9]+)*) *\[STATS:FM:BATCHES:[0-9]+\]/, m) {
    split(m[1], values, ",")
    for (i in values) {
        batchesSizesA[i] += values[i]
        batchesSizesF[i] += values[i]
        if (level == 0) {
            batchesSizesC[i] += values[i]
        }
    }
}
match($0, /Gain by distance: *([\-0-9]+(,[\-0-9]+)*) *\[STATS:FM:BATCHES:[0-9]+\]/, m) {
    split(m[1], values, ",")
    for (i in values) {
        batchesGainsA[i] += values[i]
        batchesGainsF[i] += values[i]
        if (level == 0) {
            batchesGainsC[i] += values[i]
        }
    }
}

END {
    yield()
}

function header() {
    printf "Graph,"
    printf "K,"
    printf "Seed,"
    printf "Cut,"
    printf "Epsilon,"
    printf "Balance,"
    printf "Time,"
    printf "NumNodes,"
    printf "NumMPIsPerNode,"
    printf "NumThreadsPerMPI,"

    printf "Phase,"
    printf "BatchesMaxDistance,"
    printf "Distance,"
    printf "BatchesSize,"
    printf "BatchesGain,"

    printf "Failed\n"
}

function yield() {
    if (length(data) == 0) { return }

    for (i in batchesSizesA) {
        printf "%s,", data["Graph"]
        printf "%d,", data["K"]
        printf "%d,", data["Seed"]
        printf "%d,", data["Cut"]
        printf "%f,", data["Epsilon"]
        printf "%f,", data["Balance"]
        printf "%f,", data["Time"]
        printf "%d,", data["NumNodes"]
        printf "%d,", data["NumMPIsPerNode"]
        printf "%d,", data["NumThreadsPerMPI"]
        printf "All,"
        printf "%d,", batchesMaxDistanceA
        printf "%d,", i - 1
        printf "%s,", batchesSizesA[i]
        printf "%s,", batchesGainsA[i]
        printf "%d\n", (data["Time"] == "")
    }

    for (i in batchesSizesC) {
        printf "%s,", data["Graph"]
        printf "%d,", data["K"]
        printf "%d,", data["Seed"]
        printf "%d,", data["Cut"]
        printf "%f,", data["Epsilon"]
        printf "%f,", data["Balance"]
        printf "%f,", data["Time"]
        printf "%d,", data["NumNodes"]
        printf "%d,", data["NumMPIsPerNode"]
        printf "%d,", data["NumThreadsPerMPI"]
        printf "Coarsest,"
        printf "%d,", batchesMaxDistanceC
        printf "%d,", i - 1
        printf "%s,", batchesSizesC[i]
        printf "%s,", batchesGainsC[i]
        printf "%d\n", (data["Time"] == "")
    }

    for (i in batchesSizesF) {
        printf "%s,", data["Graph"]
        printf "%d,", data["K"]
        printf "%d,", data["Seed"]
        printf "%d,", data["Cut"]
        printf "%f,", data["Epsilon"]
        printf "%f,", data["Balance"]
        printf "%f,", data["Time"]
        printf "%d,", data["NumNodes"]
        printf "%d,", data["NumMPIsPerNode"]
        printf "%d,", data["NumThreadsPerMPI"]
        printf "Finest,"
        printf "%d,", batchesMaxDistanceF
        printf "%d,", i - 1
        printf "%s,", batchesSizesF[i]
        printf "%s,", batchesGainsF[i]
        printf "%d\n", (data["Time"] == "")
    }

    reset()
}

function implode(array, char) {
    if (length(array) == 0) {
        return ""
    }

    ans = array[1]
    for (i in array) {
        if (i > 1) {
            ans = ans char array[i]
        }
    }
    return ans
}

function reset() {
    split("", data)
    batchesMaxDistanceC = -1
    batchesMaxDistanceA = -1
    split("", batchesSizesC)
    split("", batchesSizesA)
    split("", batchesGainsC)
    split("", batchesGainsA)
    resetBatchesF()
    level = -1
}

function resetBatchesF() {
    batchesMaxDistanceF = -1
    split("", batchesSizesF)
    split("", batchesGainsF)
}
